/*
 * Copyright (C) 2019 Guthix
 *
 * This program is free software; you can redistribute it and/or
 * modify it under the terms of the GNU Lesser General Public
 * License as published by the Free Software Foundation; either
 * version 3 of the License, or (at your option) any later version.
 *
 * This program is distributed in the hope that it will be useful,
 * but WITHOUT ANY WARRANTY; without even the implied warranty of
 * MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the GNU
 * Lesser General Public License for more details.
 *
 * You should have received a copy of the GNU Lesser General Public License
 * along with this program; if not, write to the Free Software Foundation,
 * Inc., 51 Franklin Street, Fifth Floor, Boston, MA  02110-1301, USA.
 */
plugins {
    id 'org.jetbrains.kotlin.jvm' version '1.3.50'
    id 'maven-publish'
    id 'java-library'
}

group 'io.guthix.buffer'
version '0.1'

ext {
    nettyVersion = '4.1.39.Final'
    kotlintestVersion = '3.4.2'
    logbackVersion = '1.2.3'
}

repositories {
    mavenCentral()
}

dependencies {
    implementation group: 'org.jetbrains.kotlin', name: 'kotlin-stdlib-jdk8'
    api group: 'io.netty', name: 'netty-buffer', version: nettyVersion
    testImplementation group: 'io.kotlintest', name: 'kotlintest-runner-junit5', version: kotlintestVersion
    testImplementation group: 'ch.qos.logback', name: 'logback-classic', version: logbackVersion
}

compileKotlin {
    kotlinOptions.jvmTarget = "11"
}

compileTestKotlin {
    kotlinOptions.jvmTarget = "11"
}

test {
    useJUnitPlatform()
}

task sourcesJar(type: Jar) {
    archiveClassifier = 'sources'
    from sourceSets.main.allSource
}

task javadocJar(type: Jar) {
    archiveClassifier = 'javadoc'
    from javadoc.destinationDir
}

publishing {
    publications {
        register(MavenPublication) {
            from components.java
            artifact sourcesJar
            artifact javadocJar
            pom {
                name = 'Jagex-ByteBuf'
                description = 'A library for reading Jagex types from a Netty ByteBuf'
                url = "https://github.com/guthix/Jagex-ByteBuf.git"
                licenses {
                    license {
                        name = 'GNU Lesser General Public License v3.0'
                        url = 'https://www.gnu.org/licenses/lgpl-3.0.txt'
                    }
                }
            }
        }
    }
}

javadoc {
    if(JavaVersion.current().isJava9Compatible()) {
        options.addBooleanOption('html5', true)
    }
}